"=============================================================================
"                               common config                                "
"=============================================================================
" be iMproved, required
set nocompatible
" set search highlight
set hlsearch
" set mapleader
let mapleader=","
" set backspace
set backspace=indent,eol,start
function! OpenVim(mode,exec)
    if has("gui_running") && a:exec == "vim"
        echohl WarningMsg
        echo "Error: Cannot switch to non-GUI mode !"
        echohl None
        return
    endif
    echo "Switching to ".a:mode." ".a:exec." ..."
    if $SHELL =~ ".*zsh$" || $SHELL =~ ".*bash$"
        silent exe "!export VIM_MODE=".a:mode."; ".a:exec." ".expand('%:p')
    elseif $SHELL =~ ".*csh$"
        silent exe "!setenv VIM_MODE ".a:mode."; ".a:exec." ".expand('%:p')
    endif
    if len(filter(range(1,bufnr('$')),'buflisted(v:val)'))=="1"
        silent exe "quit"
    else
        silent exe "bwipeout"
    endif
endfunction
" open in full mode
nnoremap <silent> <leader>v :call OpenVim("full", "gvim")<CR>
" open in light mode
nnoremap <silent> <leader>V :call OpenVim("light", "gvim")<CR>
" open in plain mode
nnoremap <silent> <leader><c-v> :call OpenVim("plain", "gvim")<CR>
" force quit
nnoremap <leader>q :q!<CR>
"=============================================================================
"                          full & light mode config                          "
"=============================================================================

if has("unix") && exists("$VIM_MODE") && $VIM_MODE != "plain" || has("win32") || has("macunix")
    " deoplete | ncm2 | coc
    let g:complete_plugin = 'ncm2'
    "==================
    "  vundle plugin  "
    "==================
    if has("win32")
        call plug#begin($vim."/vimfiles/bundle")
    else
        call plug#begin('~/.vim/bundle')
    endif
    Plug 'scrooloose/nerdcommenter'
    Plug 'Yggdroot/LeaderF'
    Plug 'xuhdev/SingleCompile'
    Plug 'altercation/vim-colors-solarized'
    Plug 'NLKNguyen/papercolor-theme'
    Plug 'Raimondi/delimitMate'
    Plug 'w0rp/ale'
    Plug 'prabirshrestha/vim-lsp'
    Plug 'prabirshrestha/async.vim'
    if g:complete_plugin == 'coc'
        Plug 'neoclide/coc.nvim', {'branch': 'release'}
    elseif g:complete_plugin == 'deoplete'
        Plug 'Shougo/deoplete.nvim'
        Plug 'Shougo/deoplete-vim-lsp'
        Plug 'roxma/vim-hug-neovim-rpc'
        Plug 'roxma/nvim-yarp'
    elseif g:complete_plugin == "ncm2"
        Plug 'ncm2/ncm2'
        Plug 'ncm2/ncm2-bufword'
        Plug 'ncm2/ncm2-path'
        Plug 'ncm2/ncm2-ultisnips'
        Plug 'yuki-ycino/ncm2-dictionary'
        Plug 'ncm2/ncm2-vim-lsp'
        Plug 'roxma/vim-hug-neovim-rpc'
        Plug 'roxma/nvim-yarp'
    endif
    
    Plug 'Tian-Changsong/tcl_vim_indent',{'for': 'tcl'}
    if g:complete_plugin != 'coc'
        "Plug 'autozimu/LanguageClient-neovim', {
                    "\ 'branch': 'next',
                    "\ 'do': 'bash install.sh',
                    "\ }
    endif
    Plug 'vim-airline/vim-airline'
    Plug 'vim-airline/vim-airline-themes'
    Plug 'SirVer/ultisnips'
    Plug 'honza/vim-snippets' " requred by ultisnips
    Plug 'junegunn/vim-easy-align'
    if has("unix") || has("macunix")
        Plug 'powerman/vim-plugin-viewdoc'
    endif
    Plug 'Yggdroot/indentLine',{'for': ['tcl', 'python']}
    call plug#end()
    " turn on filetype detection, ftplugin, indent,vim-plug has done it
    "filetype plugin indent on
    "================
    "  key mapping  "
    "================
    if has("gui_running")
        " set CTRL+s to save if gvim
        inoremap <C-s> <esc>:w<CR>a
        nnoremap <C-s> :w<CR>
        " set CTRL+c to copy in system's clipboard, use shift+insert to paste
        vmap <C-c> "+y
    endif
    " buffer swap
    nnoremap <s-h> :bp<CR>
    nnoremap <s-l> :bn<CR>
    " Wrapped lines goes down/up to next row, rather than next line in file.
    noremap j gj
    noremap k gk
    " Yank from the cursor to the end of the line, to be consistent with C and D.
    noremap Y y$
    " automatically highlight word under cursor
    autocmd CursorMoved * exe exists("HlUnderCursor")?HlUnderCursor?printf('match IncSearch /\V\<%s\>/', escape(expand('<cword>'),'/\')):'match none':""
    nnoremap <silent> <leader>h :exe "let HlUnderCursor=exists(\"HlUnderCursor\")?HlUnderCursor*-1+1:1"<CR>
    " Visual shifting (does not exit Visual mode)
    vnoremap < <gv
    vnoremap > >gv
    " Easier horizontal scrolling
    map zl zL
    map zh zH
    " toggle wrap
    nnoremap <leader>w :set wrap!<CR>
    " test if file exists
    function! TestExists(mode)
        if a:mode=="normal"
            let s:filename=expand("<cfile>")
        elseif a:mode=="visual"
            let s:filename=getreg('*',1)
        endif
        if !empty(glob(s:filename))
            echohl MoreMsg
            echo "Exists !(File: ".s:filename.")"
            echohl None
        else
            echohl WarningMsg
            echo "Does not exists !(File: ".s:filename.")"
            echohl None
        endif
    endfunction

    nnoremap <silent> te :call TestExists("normal")<CR>
    vnoremap <silent> te <Esc>:call TestExists("visual")<CR>
    " make file executable
    function! MakeExecutable()
        let filename=expand("%:p")
        if executable(filename) == 0
            silent exe "!chmod a+x ".shellescape(filename)
            if v:shell_error
                echohl WarningMsg
                echo 'Could not make this file executable!'
                echohl None
            else
                if ! &l:modified
                    silent exe "edit"
                endif
                echohl MoreMsg
                echo "Made this file executable!"
                echohl None
            endif
        else
            silent exe "!chmod -x ".shellescape(filename)
            if v:shell_error
                echohl WarningMsg
                echo 'Could not make this file not executable!'
                echohl None
            else
                if ! &l:modified
                    silent exe "edit"
                endif
                echohl WarningMsg
                echo "Make this file not executable!"
                echohl None
            endif
        endif
    endfunction
    nnoremap <silent> <leader>x :call MakeExecutable()<CR>
    " toggle show menu bar
    map <silent> <F2> :if &guioptions =~# 'm' <Bar> set guioptions-=m <Bar> else <Bar> set guioptions+=m <Bar> endif <CR>
    " open configuration file with short key
    if has("win32")
        nnoremap <leader>ev :e $vim/_vimrc<CR>
    else
        nnoremap <leader>ev :e $HOME/.vimrc<CR>
        nnoremap <leader>ec :e $HOME/.cshrc<CR>
    endif
    " paste from system clipboard
    if has("gui_running")
        imap <S-Insert> <esc>"+pa
    endif
    " save buffer
    nnoremap <leader>s :w<CR>
    " use tab to choose item im pop menu
    inoremap <expr> <Tab> pumvisible() ? "\<C-n>" : "\<C-g>u\<Tab>"
    inoremap <expr> <S-Tab> pumvisible() ? "\<C-p>" : "\<C-g>u\<Tab>"
    " open folding with space
    nnoremap <space> @=((foldclosed(line('.')) < 0) ? 'zc' : 'zo')<CR>
    " force quit
    nnoremap <leader>q :q!<CR>
    let s:minfontsize = 6
    let s:maxfontsize = 18
    if has("win32") || has("macunix")
        let s:pattern = '^\(.*:h\)\([0-9]\+\)'
    else
        let s:pattern = '^\(.* \)\([0-9]\+\)$'
    endif
    function! AdjustFontSize(amount)
        if has("gui_running")
            let fontname = substitute(&guifont, s:pattern, '\1', '')
            let cursize = substitute(&guifont, s:pattern, '\2', '')
            let newsize = cursize + a:amount
            let newfont = fontname . newsize
            echohl MoreMsg
            if (newsize < s:minfontsize)
                echo "Font size cannot be less than " . s:minfontsize . " !"
            elseif (newsize > s:maxfontsize)
                echo "Font size cannot be greater than " . s:maxfontsize . " !"
            else
                let &guifont = newfont | redraw | echo "Font changed to '" . newfont ."'"
            endif
            echohl None
        endif
    endfunction

    function! LargerFont()
        call AdjustFontSize(1)
    endfunction
    command! LargerFont call LargerFont()

    function! SmallerFont()
        call AdjustFontSize(-1)
    endfunction
    command! SmallerFont call SmallerFont()
    map <silent><c-Up> :call AdjustFontSize(1)<CR>
    map <silent><c-Down> :call AdjustFontSize(-1)<CR>

    function! ToggleFont(mode)
        if a:mode=="shrink"
            if has("win32")
                set guifont=DejaVu_Sans_Mono_for_Powerline:h9
            else
                set guifont=DejaVu\ Sans\ Mono\ for\ Powerline\ 9
            endif
            set linespace=0
        elseif a:mode=="enlarge"
            if has("win32")
                set guifont=DejaVu_Sans_Mono_for_Powerline:h12
            else
                set guifont=DejaVu\ Sans\ Mono\ for\ Powerline\ 12
            endif
            set linespace=0
        endif
    endfunction
    " fullscreen or maximize
    if has("gui_running")
        if has("unix")
            function! ToggleFullScreen()
                call system("wmctrl -r :ACTIVE: -b toggle,fullscreen")
            endfunction

            function! MaximizeWin()
                call system("wmctrl -r :ACTIVE: -b add,maximized_vert,maximized_horz")
            endfunction

            nnoremap <F11> :call ToggleFullScreen()<CR>
            nnoremap <silent> <F4> :call MaximizeWin()<CR>:call ToggleFont("shrink")<CR>
            nnoremap <silent> <c-F4> :call ToggleFont("enlarge")<CR>
        else
            nnoremap <silent> <F4> :call ToggleFont("shrink")<CR>
            nnoremap <silent> <c-F4> :call ToggleFont("enlarge")<CR>
        endif
    endif
    " backup current line
    nnoremap <leader>y m'yyP :call NERDComment(0,"toggle")<CR>`'
    " Delete trailing white space on save, useful for Python and CoffeeScript
    nnoremap <leader>S :%s/\s\+$//ge<CR>
    " toggle highlight search
    nmap <silent> <leader>/ :set hlsearch!<CR>
    " Visual mode pressing * or # searches for the current selection
    vnoremap  * y/<C-R>=escape(@", '\\/.*$^~[]')<CR><CR>
    vnoremap  # y?<C-R>=escape(@", '\\/.*$^~[]')<CR><CR>
    " Visual mode pressing <leader>n to get count of current selection
    vnoremap <silent> <leader>n y:%s/<C-R>=escape(@", '\\/.*$^~[]')<CR>//gn<CR>
    " When you press gv you vimgrep after the selected text
    vnoremap gv y:vimgrep /<C-R>=escape(@", '\\/.*$^~[]')<CR>/ **/*
    " When you press <leader>h you can search and replace the selected text
    vnoremap <leader>h y:%s/<C-R>=escape(@", '\\/.*$^~[]')<CR>//g<Left><Left>
    vnoremap <leader>H y:%s/\<<C-R>=escape(@", '\\/.*$^~[]')<CR>\>//g<Left><Left>
    " reformat
    noremap <F9> gggqG
    "================
    "  vim options  "
    "================
    " keep indention of previous line
    set autoindent
    " show cursor position
    set ruler
    " set 7 lines to the cursor when moving vertically using j/k
    "set scrolloff=7
    " Don't redraw while executing macros (good performance config)
    set lazyredraw
    " change buffer without saving
    set hidden
    " do not blink
    set novisualbell
    " no swap file
    set noswapfile
    " linespace
    set linespace=1
    " don't ring error bell
    set noerrorbells
    " set mouse working like in xterm
    set mousemodel=extend
    " history length
    set history=50
    " show command just excuted
    set showcmd
    " use system clipboard when pressing y in Windows
    if has("win32")
        set clipboard=unnamed
    endif
    " show completion for commands
    set wildmenu
    " ignore case when searching
    set ignorecase smartcase
    " do not show file path on tab
    set guitablabel=[%N]\ %m%t
    " color theme
    if has("gui_running")
        if $VIM_MODE == "full"
            set background=dark
            colorscheme solarized
        elseif $VIM_MODE == "light"
            set background=light
            colorscheme PaperColor
        else
            set background=dark
            colorscheme solarized
        endif
    else
        set background=dark
        colorscheme solarized
    endif
    " set expandtab
    set expandtab
    " set font
    if has("win32")
        set guifont=DejaVu_Sans_Mono_for_Powerline:h12
        set guifontwide=DejaVu_Sans_Mono_for_Powerline:h12
    elseif has("macunix")
        set guifont=Monaco\ for\ Powerline:h13
        set guifontwide=Monaco\ for\ Powerline:h13
    elseif has("unix")
        set guifont=DejaVu\ Sans\ Mono\ for\ Powerline\ 12
        set guifontwide=UbuntuMono\ Nerd\ Font\ Mono\ 12
    endif
    " set encoding
    set fileencodings=utf-8,gb18030,gbk,gb2312,cp936,ucs-bom
    set termencoding=utf-8
    set encoding=utf-8
    " avoid messy code
    if has("win32")
        set langmenu=zh_CN.UTF-8
        language messages zh_CN.UTF-8
    endif
    source $VIMRUNTIME/delmenu.vim
    source $VIMRUNTIME/menu.vim
    " show line number
    set number
    " new vertically split window is no the right
    set splitright
    " no horizontally split window is below old
    set splitbelow
    " enable syntax highlight; vim-plug has done it
    "syntax on
    " set fileformats
    set fileformats=unix,dos
    " set tab width
    set tabstop=4
    set softtabstop=4
    " set indention width
    set shiftwidth =4
    " set libe break width
    set textwidth=0
    " set max tab number
    set tabpagemax=20
    " set current file's path as pwd
    "set autochdir
    " set current line highlight
    if has("gui_running")
        set cursorline
    endif
    " set incremental search
    set incsearch
    "set cursorcolumn
    " set enable folding
    set foldenable
    " set fold method
    set foldmethod=indent
    "set foldcolumn=0
    set foldlevel=20
    "set foldclose=all
    " use mouse
    set mouse=a
    " do not backup
    set nobackup
    " set dictionary
    autocmd filetype tcl setlocal dictionary=~/.vim/my_dictionary/tcl.dict
    " complete words with dot while dictionary completion
    "set iskeyword +=.
    " set local filetype
    autocmd filetype conf setlocal filetype=tcl
    augroup filetypedetect
        au BufRead,BufNewFile *.upf set filetype=tcl
        au BufRead,BufNewFile *.cpf set filetype=tcl
        au BufRead,BufNewFile *.lib set filetype=c | setlocal iskeyword=@,48-57,_,192-255
    augroup END
    " add scroll bar on left and bottom
    set guioptions +=Lb
    " hide menu and tool bar
    set guioptions -=T
    set guioptions -=m
    " remember last cursor position
    autocmd BufReadPost *
                \ if line("'\"") > 0 && line("'\"") <= line("$") |
                \ exe "normal g`\"" |
                \ endif
    " set filetype to text if filetype is empty
    autocmd FileWritePost,BufWritePost,BufNewFile,BufReadPost *
            \ if &filetype == "" |
            \ setlocal filetype=text |
            \ endif
    " insert time
    iab xtime <c-r>=strftime("%Y-%m-%d %H:%M:%S")
    " window size when using gvim
    if has("gui_running")
        set lines=100 columns=150
    endif
    " format programs
    au Filetype python set formatprg=autopep8\ -
    au Filetype perl set formatprg=perltidy

    " capture ex command message
    function! BufMessage(cmd)
        redir => message
        silent execute a:cmd
        redir END
        edit exMessage
        silent put=message
        set nomodified
    endfunction
    command! -nargs=+ -complete=command BufMessage call BufMessage(<q-args>)

    "====================
    "  plugin settings  "
    "====================
    " "ultisnips"
    let g:UltiSnipsExpandTrigger="<c-j>"
    " open snippets
    nnoremap <leader>es :UltiSnipsEdit<CR>
    if has("win32")
        let g:UltiSnipsSnippetDirectories=[$vim.'/vimfiles/my_snippets']
    else
        let g:UltiSnipsSnippetDirectories=[$HOME."/.vim/my_snippets"]
    endif

    " "airline"
    set laststatus=2
    set t_Co=256
    let g:airline#extensions#whitespace#enabled = 0
    if !exists('g:airline_symbols')
        let g:airline_symbols={}
    endif
    let g:airline_symbols.maxlinenr = ''
    let g:airline_powerline_fonts=1

    let g:airline#extensions#tabline#enabled = 1
    let g:airline#extensions#tabline#tab_nr_type = 1
    let g:airline#extensions#tabline#formatter = 'unique_tail'
    let g:airline#extensions#tabline#buffer_idx_mode = 1
    "map <leader>n to select buffer
    nmap <leader>1 <Plug>AirlineSelectTab1
    nmap <leader>2 <Plug>AirlineSelectTab2
    nmap <leader>3 <Plug>AirlineSelectTab3
    nmap <leader>4 <Plug>AirlineSelectTab4
    nmap <leader>5 <Plug>AirlineSelectTab5
    nmap <leader>6 <Plug>AirlineSelectTab6
    nmap <leader>7 <Plug>AirlineSelectTab7
    nmap <leader>8 <Plug>AirlineSelectTab8
    nmap <leader>9 <Plug>AirlineSelectTab9

    " "SingleCompile"
    nmap <F10> :SCCompileRun<CR>
    nmap <S-F10> :SCViewResult<CR>

    " "vim-viewdoc"
    let g:viewdoc_open='new'
    let g:viewdoc_only=1
    let g:viewdoc_man_cmd="man -a"
    function s:ViewDoc_tcl(topic, ...)
        return { 'cmd': printf('man -M /home/changsongtian/programs/tcl/man %s | col -b', shellescape(a:topic,1)),
            \ 'ft': 'tcl',
            \ }
    endfunction
    let g:ViewDoc_tcl = function('s:ViewDoc_tcl')

    " "indentLine"
    let g:indentLine_enabled=0
    nnoremap <leader>i :IndentLinesToggle<CR>

    " "vim-easy-align"
    vmap ga <Plug>(EasyAlign)
    vmap <Enter> <Plug>(EasyAlign)

    " "deoplete"
    if g:complete_plugin == "deoplete"
        " Enable deoplete when InsertEnter.
        let g:deoplete#enable_at_startup = 0
        autocmd InsertEnter * call deoplete#enable()
        autocmd InsertLeave * silent! pclose!
        call deoplete#custom#source('ultisnips', 'rank', 1000)
        if has("win32")
            let g:python3_host_prog="D:/Python36/python.exe"
        else
            "if echo exepath('python3') is not empty, no need to set this variable
            "let g:python3_host_prog="D:/Python36/python.exe"
        endif
    endif


    " "LeaderF"
    let g:Lf_StlSeparator = { 'left': '', 'right': '' }
    let g:Lf_WorkingDirectoryMode='F'
    let g:Lf_FollowLinks=1
    " use ripgrep, or platinum searcher
    if has("unix") && executable('rg')
        "let g:Lf_ExternalCommand = 'find "%s" -type f'
        let g:Lf_ExternalCommand = 'rg "%s" --no-messages --files --hidden'
    else
        let g:Lf_ExternalCommand = 'find "%s" -type f'
    endif
    map <leader>r :LeaderfMru<CR>
    map <leader>F :LeaderfFile<CR>
    function! LeaderfCurrentDir()
        let g:Lf_UseCache=0
        let g:Lf_UseMemoryCache=0
        let s:Lf_ExternalCommand= g:Lf_ExternalCommand
        let g:Lf_WorkingDirectoryMode='c'
        if expand("%:~:.:h") == ""
            let g:Lf_ExternalCommand='echo "%s" ; find -L . -maxdepth 1 -type f'
        else 
            let g:Lf_ExternalCommand='echo "%s" ; find -L '.expand("%:~:.:h").' -maxdepth 1 -type f'
        endif
        execute 'LeaderfFile'
        let g:Lf_ExternalCommand=s:Lf_ExternalCommand
        let g:Lf_UseCache=1
        let g:Lf_WorkingDirectoryMode='F'
    endfunction
    map <leader>f :call LeaderfCurrentDir()<CR>


    " "LanguageClient-neovim"
    if g:complete_plugin != 'coc'
        let g:LanguageClient_serverCommands = {
                    \ 'python': ['pyls'],
                    \ }
    endif

    " "ncm2"
    if g:complete_plugin == "ncm2"
        autocmd InsertEnter * call ncm2#enable_for_buffer()
        "" IMPORTANT: :help Ncm2PopupOpen for more information
        set completeopt=noinsert,menuone,noselect
    endif

    " "vim-lsp"
    if executable('pyls')
        " pip install python-language-server
        au User lsp_setup call lsp#register_server({
                    \ 'name': 'pyls',
                    \ 'cmd': {server_info->['pyls']},
                    \ 'whitelist': ['python'],
                    \ })
    endif
    " use ale to show marks
    let g:lsp_diagnostics_enabled=0
else
"=============================================================================
"                             plain mode config                              "
"=============================================================================
    " enable syntax highlight
    syntax on
endif
